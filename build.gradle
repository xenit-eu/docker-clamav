plugins {
    id 'eu.xenit.docker' version '5.3.2'
    id 'be.vbgn.ci-detect' version '0.5.0'
}

createDockerFile {
    var configFile = 'image/clamd.conf'
    from 'docker.io/clamav/clamav:1.4.3'
    smartCopy configFile, '/etc/clamav/'
}

dockerBuild {
    repositories = ['docker.io/xenit/clamav']
    tags = generateImageTags()
}

docker {
    registryCredentials {
        username = System.getenv('DOCKER_USER')
        password = System.getenv('DOCKER_PASSWORD')
    }
}

def generateImageTags() {
    String timestamp = new Date().format('yyyyMMddHHmmss')

    String branch = ci.reference ?: 'local'
    if (!branch.startsWithAny('master', 'main'))
        // Replace all non-alphanumeric characters by '_' to sanitize for image tag
        return [branch.replaceAll('\\W', '_') + '-' + timestamp]
    return [timestamp]
}
